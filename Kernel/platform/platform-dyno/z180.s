# 1 "z180.S"
# 1 "../../cpu-z180/z180.S"
; 2014-12-19 William R Sowerbutts
; An attempt at generic Z180 support code, based on N8VEM Mark IV and DX-Design P112 targets

        .z180
# 1 "../../cpu-z180/../build/kernelu.def"
; FUZIX mnemonics for memory addresses etc

U_DATA__TOTALSIZE       .equ 0x200        ; 256+256 bytes @ F800

OS_BANK                 .equ 0x00         ; value from include/kernel.h

; Memory layout
FIRST_RAM_BANK          .equ 0x80         ; low 512K of physical memory is ROM/ECB window.
Z180_IO_BASE            .equ 0xC0

USE_FANCY_MONITOR       .equ 1            ; disabling this saves around approx 0.5KB
CPU_CLOCK_KHZ           .equ 18432        ; 18.432MHz * 1
Z180_TIMER_SCALE        .equ 20           ; CPU clocks per timer tick
TICKSPERSEC             .equ 40           ; timer interrupt rate (Hz)

PROGBASE		.equ 0x0000
PROGLOAD		.equ 0x0100



FDC_MSR			.equ	0x84
FDC_DATA		.equ	0x85
FDC_DOR			.equ	0x86
FDC_CCR			.equ	0x87
FDC_TC			.equ	0x86	  ; TC is a read



PPIDE_RD_LINE	.equ	0x40
PPIDE_WR_LINE	.equ	0x20
PPIDE_PPI_BUS_READ	.equ	0x92
PPIDE_PPI_BUS_WRITE	.equ	0x80

ppi_port_a	.equ	0x4C
ppi_port_b	.equ	0x4D
ppi_port_c	.equ	0x4E
ppi_control	.equ	0x4F

ppide_data	.equ	0x08
# 1 "../../cpu-z180/../cpu-z180/z180.def"
; ASCI serial ports
ASCI_CNTLA0                 .equ Z180_IO_BASE+0x00     ; ASCI control register A channel 0
ASCI_CNTLA1                 .equ Z180_IO_BASE+0x01     ; ASCI control register A channel 1
ASCI_CNTLB0                 .equ Z180_IO_BASE+0x02     ; ASCI control register B channel 0
ASCI_CNTLB1                 .equ Z180_IO_BASE+0x03     ; ASCI control register B channel 0
ASCI_STAT0                  .equ Z180_IO_BASE+0x04     ; ASCI status register    channel 0
ASCI_STAT1                  .equ Z180_IO_BASE+0x05     ; ASCI status register    channel 1
ASCI_TDR0                   .equ Z180_IO_BASE+0x06     ; ASCI transmit data reg, channel 0
ASCI_TDR1                   .equ Z180_IO_BASE+0x07     ; ASCI transmit data reg, channel 1
ASCI_RDR0                   .equ Z180_IO_BASE+0x08     ; ASCI receive data reg,  channel 0
ASCI_RDR1                   .equ Z180_IO_BASE+0x09     ; ASCI receive data reg,  channel 0
ASCI_ASEXT0                 .equ Z180_IO_BASE+0x12     ; ASCI extension register channel 0
ASCI_ASEXT1                 .equ Z180_IO_BASE+0x13     ; ASCI extension register channel 1
ASCI_ASTC0L                 .equ Z180_IO_BASE+0x1A     ; ASCI time constant register channel 0 low
ASCI_ASTC0H                 .equ Z180_IO_BASE+0x1B     ; ASCI time constant register channel 0 high
ASCI_ASTC1L                 .equ Z180_IO_BASE+0x1C     ; ASCI time constant register channel 1 low
ASCI_ASTC1H                 .equ Z180_IO_BASE+0x1D     ; ASCI time constant register channel 1 high

; Z180 MMU
MMU_CBR                     .equ Z180_IO_BASE+0x38     ; common1 base register
MMU_BBR                     .equ Z180_IO_BASE+0x39     ; bank base register
MMU_CBAR                    .equ Z180_IO_BASE+0x3A     ; common/bank area register

; Z180 DMA engine
DMA_SAR0L                   .equ Z180_IO_BASE+0x20     ; DMA source address reg, channel 0L
DMA_SAR0H                   .equ Z180_IO_BASE+0x21     ; DMA source address reg, channel 0H
DMA_SAR0B                   .equ Z180_IO_BASE+0x22     ; DMA source address reg, channel 0B
DMA_DAR0L                   .equ Z180_IO_BASE+0x23     ; DMA dest address reg,   channel 0L
DMA_DAR0H                   .equ Z180_IO_BASE+0x24     ; DMA dest address reg,   channel 0H
DMA_DAR0B                   .equ Z180_IO_BASE+0x25     ; DMA dest address reg,   channel 0B
DMA_BCR0L                   .equ Z180_IO_BASE+0x26     ; DMA byte count reg,     channel 0L
DMA_BCR0H                   .equ Z180_IO_BASE+0x27     ; DMA byte count reg,     channel 0H
DMA_MAR1L                   .equ Z180_IO_BASE+0x28     ; DMA memory address reg, channel 1L
DMA_MAR1H                   .equ Z180_IO_BASE+0x29     ; DMA memory address reg, channel 1H
DMA_MAR1B                   .equ Z180_IO_BASE+0x2A     ; DMA memory address reg, channel 1B
DMA_IAR1L                   .equ Z180_IO_BASE+0x2B     ; DMA I/O address reg,    channel 1L
DMA_IAR1H                   .equ Z180_IO_BASE+0x2C     ; DMA I/O address reg,    channel 1H
DMA_BCR1L                   .equ Z180_IO_BASE+0x2E     ; DMA byte count reg,     channel 1L
DMA_BCR1H                   .equ Z180_IO_BASE+0x2F     ; DMA byte count reg,     channel 1H
DMA_DSTAT                   .equ Z180_IO_BASE+0x30     ; DMA status register
DMA_DMODE                   .equ Z180_IO_BASE+0x31     ; DMA mode register
DMA_DCNTL                   .equ Z180_IO_BASE+0x32     ; DMA/WAIT control register

; Z180 Timer
TIME_TMDR0L                 .equ Z180_IO_BASE+0x0C     ; Timer data register,    channel 0L
TIME_TMDR0H                 .equ Z180_IO_BASE+0x0D     ; Timer data register,    channel 0H
TIME_RLDR0L                 .equ Z180_IO_BASE+0x0E     ; Timer reload register,  channel 0L
TIME_RLDR0H                 .equ Z180_IO_BASE+0x0F     ; Timer reload register,  channel 0H
TIME_TCR                    .equ Z180_IO_BASE+0x10     ; Timer control register
TIME_TMDR1L                 .equ Z180_IO_BASE+0x14     ; Timer data register,    channel 1L
TIME_TMDR1H                 .equ Z180_IO_BASE+0x15     ; Timer data register,    channel 1H
TIME_RLDR1L                 .equ Z180_IO_BASE+0x16     ; Timer reload register,  channel 1L
TIME_RLDR1H                 .equ Z180_IO_BASE+0x17     ; Timer reload register,  channel 1H
TIME_FRC                    .equ Z180_IO_BASE+0x18     ; Timer Free running counter

; Z180 Interrupts
INT_IL                      .equ Z180_IO_BASE+0x33     ; Interrupt vector low register
INT_ITC                     .equ Z180_IO_BASE+0x34     ; Interrupt vector low register

; Refresh control
MEM_RCR			    .equ Z180_IO_BASE+0x36	; Refresh control

; ESCC serial ports (Z80182)
ESCC_CTRL_A                 .equ 0xE0                   ; ESCC Channel A control register
ESCC_DATA_A                 .equ 0xE1                   ; ESCC Channel A data register
ESCC_CTRL_B                 .equ 0xE2                   ; ESCC Channel B control register
ESCC_DATA_B                 .equ 0xE3                   ; ESCC Channel B data register

PORT_A_DDR                  .equ 0xED                   ; Port A data direction register
PORT_A_DATA                 .equ 0xEE                   ; Port A data register
PORT_B_DDR                  .equ 0xE4                   ; Port B data direction register
PORT_B_DATA                 .equ 0xE5                   ; Port B data register
PORT_C_DDR                  .equ 0xDD                   ; Port C data direction register
PORT_C_DATA                 .equ 0xDE                   ; Port C data register

Z182_SYSCONFIG              .equ 0xEF                   ; System Configuration Register
Z182_RAMUBR                 .equ 0xE6                   ; RAM upper boundary register
Z182_RAMLBR                 .equ 0xE7                   ; RAM lower boundary register
Z182_ROMBR                  .equ 0xE8                   ; ROM boundary register

; Debugging
DEBUGBANK   .equ 0
DEBUGCOMMON .equ 0
# 1 "../../cpu-z180/../cpu-z80u/kernel-z80.def"
 
# 26
 
# 44
 
# 10 "../../cpu-z180/z180.S"
        ; exported symbols
        .export z180_init_hardware
        .export z180_init_early
        .export _program_vectors
        .export _copy_and_map_proc
        .export interrupt_table ; not used elsewhere but useful to check correct alignment
        .export hw_irqvector
        .export _irqvector
        .export _need_resched
	.export _int_disabled

	.export map_kernel_restore
	.export map_buffers
	.export map_kernel_di
	.export map_kernel
	.export map_proc_always_di
	.export map_proc_always
	.export map_for_swap
	.export map_save_kernel
	.export map_restore

	.export _plt_switchout
	.export _switchin
	.export _dofork


	.export _swapout



OS_RAM1	.equ	FIRST_RAM_BANK + 0           
RAM64K	.equ	FIRST_RAM_BANK + 0x20
TSCALE	.equ	1000/Z180_TIMER_SCALE

; -----------------------------------------------------------------------------
; Initialisation code
; -----------------------------------------------------------------------------
	.discard

z180_init_early:
        ; Assumes we enter with BBR=CBR and CBAR=x0 where x>0.
        ; If we are running in the first 64K we need to first copy ourselves elsewhere
        in0	a, (MMU_BBR)
        cp	OS_RAM1
        jr	z, dommu             ; we're in position already
        cp	OS_RAM1 + 0x10
        jr	nc, finalcopy        ; greater than -- we're running above 64K
        ; we are running at least in part in the first 64K of RAM -- copy us up to 128KB temporarily
        ld	a, RAM64K / 16
        call	copykernel
# 68
        ; reprogram the MMU to use the copy at 128KB
        ld	a, RAM64K
        out0	(MMU_BBR), a
        out0	(MMU_CBR), a
# 78
finalcopy:
        ; copy us into position
        ld	a,OS_RAM1 / 16
        call	copykernel

dommu:
# 92
        ; reprogram the MMU to use the copy at bottom of RAM
        ld	a, OS_RAM1
        out0	(MMU_BBR), a       ; low 60K
        out0	(MMU_CBR), a       ; upper 4K (including our stack)
# 101
        ; program MMU for 60KB/4KB bank/common1 split.
        ld	a,0xF0
        out0	(MMU_CBAR), a

        ret

copykernel:
        out0	(DMA_DAR0B), a

        ; HL = BBR << 4
        xor	a
        ld	h, a
        in0	l, (MMU_BBR)        ; read BBR value
        add	hl, hl              ; shift left 4 bits
        add	hl, hl
        add	hl, hl
        add	hl, hl

        ; load the DMA engine registers with source (HL<<8), destination (bank 
        ; register programmed already), and count (64KB)
        out0	(DMA_BCR0L), a     ; A=0 still
        out0	(DMA_BCR0H), a
        out0	(DMA_DAR0L), a
        out0	(DMA_DAR0H), a
        out0	(DMA_SAR0L), a
        out0	(DMA_SAR0H), l
        out0	(DMA_SAR0B), h
        ld	bc, 0x0240
        out0	(DMA_DMODE), b     ; 0x02 - memory to memory, burst mode
        out0	(DMA_DSTAT), c     ; 0x40 - enable DMA channel 0
        ; in burst mode the Z180 CPU stops until the DMA completes
        ret

z180_init_hardware:
        ; setup interrupt vectors for the kernel bank
	ld	a,0xC3
        ; Set vector for jump to NULL
        ld	(0x0000), a   
        ld	hl, null_handler  ;   to Our Trap Handler
        ld	(0x0001), hl

        ld	(0x0066), a  	; Set vector for NMI
        ld	hl, nmi_handler
        ld	(0x0067), hl

        ; program Z180 interrupt table registers
        ld	hl, interrupt_table ; note table MUST be 32-byte aligned!
        out0	(INT_IL), l
        ld	a, h
        ld	i, a
        im	1 ; set CPU interrupt mode for INT0

        ; set up system tick timer
        xor	a
        out0	(TIME_TCR), a
        ld	hl, CPU_CLOCK_KHZ*TSCALE/TICKSPERSEC-1
        out0	(TIME_RLDR0L), l
        out0	(TIME_RLDR0H), h
        ld	a, 0x11         ; enable downcounting and interrupts for timer 0 only
        out0	(TIME_TCR), a

        ; Enable illegal instruction trap (vector at 0x0000)
        ; Enable external interrupts (INT0/INT1/INT2) 
        ld	a, 0x87
        out0	(INT_ITC), a

        jp	_init_hardware_c

; -----------------------------------------------------------------------------
; KERNEL MEMORY BANK (only accessible when the kernel is mapped)
; -----------------------------------------------------------------------------
	.code

_copy_and_map_proc:
        di      	; just to be sure
        pop	bc      ; temporarily store return address
        pop	de      ; function argument -- pointer to base page number
        push	de	; put stack back as it was
        push	bc

        ; overwrites the full 64KB of target process memory space

        ; WARNING: 
        ; assumes processes page numbers are only 8-bits wide
        ; assumes kernel is physically 64K aligned
        ; assumes processes have a full 64K allocated to them

        ld	bc, 0x0240
        out0	(DMA_DMODE), b     ; 0x02 - memory to memory, burst mode

        ; load destination page number into HL
        ld	a, (de)
        ld	b, a	; stash copy in B -- BC remains unmodified hereafter
        ld	l, a
        ld	h, 0

        ; shift left 4 bits
        add	hl, hl
        add	hl, hl
        add	hl, hl
        add	hl, hl
        ; now bottom four bits of H holds the top four bits of physical address,
        ; while the top four bits of L hold the next four bits.
        out0	(DMA_DAR0B), h

        ; source bank -- kernel is always 64K aligned
        ld	a, OS_RAM1 / 16
        out0	(DMA_SAR0B), a

        ; Copy vectors -- virtual 0000 to 0080
        ld	de, 0x0080
        out0	(DMA_BCR0H), d	; 0x0080 bytes to copy
        out0	(DMA_BCR0L), e
        out0	(DMA_DAR0H), l	; computed destination page
        out0	(DMA_DAR0L), d
        out0	(DMA_SAR0H), d	; source is kernel, always 64K aligned
        out0	(DMA_SAR0L), d
        ; call dump_dma_state
        out0	(DMA_DSTAT), c	; 0x40 - enable DMA channel 0
        ; CPU stalled until DMA completes

        ; Clone 0x7F into virtual 0080 through 0100 (kernel has code here, reserved in userspace)
        ; In the future interrupt stubs may go in here for processes with less than 64K allocated
        out0	(DMA_BCR0H), d	; 0x80 bytes
        out0	(DMA_BCR0L), e
        dec	e		; 0x80 -> 0x7F
        out0	(DMA_SAR0B), h
        out0	(DMA_SAR0H), l
        out0	(DMA_SAR0L), e
        ; no need to set DAR0B, DAR0H, DAR0L since they naturally ends up there after the above copy
        ; call dump_dma_state
        out0	(DMA_DSTAT), c	; 0x40 - enable DMA channel 0
        ; CPU stalled until DMA completes

        ; Copy common memory code from kernel bank (from end of U_DATA to end of memory)
	;  ld de, #(0x10000 - U_DATA__TOTALSIZE - _udata) ; copy to end of memory
	; Only the linker isn't smart enough....
	or	a
	push	hl
	ld	hl, 0
	ld	de,_udata + U_DATA__TOTALSIZE
	sbc	hl, de
	ex	de, hl
	pop	hl
        out0	(DMA_BCR0H), d     ; set byte count
        out0	(DMA_BCR0L), e
        ld	de, _udata + U_DATA__TOTALSIZE
        ld	a, OS_RAM1 / 16    ; source bank -- kernel is always 64K aligned
        out0	(DMA_SAR0B), a
        out0	(DMA_SAR0H), d     ; source is kernel, always 64K aligned
        out0	(DMA_SAR0L), e
        ; compute dest address; (HL << 8) + DE
        out0	(DMA_DAR0L), e
        ld	a, l
        add	a, d
        out0	(DMA_DAR0H), a
        ld	a, h
        jr	nc, bankok
        inc a
bankok: out0	(DMA_DAR0B), a
        ; call dump_dma_state
        out0	(DMA_DSTAT), c	; 0x40 - enable DMA channel 0
        ; CPU stalled until DMA completes
        ; note we just overflowed at least one, possibly both DMA bank registers

        ; Copy user code (ie fill in the middle, 0x100 up to end of U_DATA) from the current process
        ; compute dest address; (HL << 8) + 0x0100
        xor	a
        out0	(DMA_DAR0L), a
        ld	a, l
        inc	a
        out0	(DMA_DAR0H), a
        ld	a, h
        jr	nc, bankok2
        inc	a
bankok2:out0 (DMA_DAR0B), a
        ; compute source address from current process 
        in0	l, (MMU_CBR)	; get current process memory address
        ld	h, 0
        add	hl, hl		; shift left 4 bits
        add	hl, hl
        add	hl, hl
        add	hl, hl
        inc	hl              ; add in 0x100 start offset
        xor	a
        out0	(DMA_SAR0L), a
        out0	(DMA_SAR0H), l
        out0	(DMA_SAR0B), h
        ld	de, _udata+U_DATA__TOTALSIZE-0x100 ; byte count
        out0	(DMA_BCR0H), d	; set byte count
        out0	(DMA_BCR0L), e
        ; call dump_dma_state
        out0	(DMA_DSTAT), c	; 0x40 - enable DMA channel 0
        ; CPU stalled until DMA completes

        ; finally reprogram the MMU to bring the new process common memory into context
        ; note this replaces the stack, but we just copied it over.
# 310
        out0	(MMU_CBR), b

        ret ; was jp map_kernel but we never change MMU_BBR

_program_vectors:
        ; copy_and_map_proc has all the fun now
        ret

fork_proc_ptr:
	.word 0 ; (C type is struct p_tab *) -- address of child process p_tab entry

;
;   Called from _fork. We are in a syscall, the uarea is live as the
;   parent uarea. The kernel is the mapped object.
;
_dofork:
        ; always disconnect the vehicle battery before performing maintenance
        di ; should already be the case ... belt and braces.

        pop	de  ; return address
        pop	hl  ; new process p_tab*
        push	hl
        push	de

        ld	(fork_proc_ptr), hl

        ; prepare return value in parent process -- HL = p->p_pid;
        ld	de, 3 
        add	hl, de
        ld	a, (hl)
        inc	hl
        ld	h, (hl)
        ld	l, a

        ; Save the stack pointer and critical registers.
        ; When this process (the parent) is switched back in, it will be as if
        ; it returns with the value of the child's pid.
        push	hl ; HL still has p->p_pid from above, the return value in the parent
	push	bc ; register variables
        push	ix
        push	iy
	; Compiler temporaries
	ld	hl,(__tmp)
	push	hl
	ld	hl,(__hireg)
	push	hl
	ld	hl,(__tmp2)
	push	hl
	ld	hl,(__tmp2+2)
	push	hl
	ld	hl,(__tmp3)
	push	hl
	ld	hl,(__tmp3+2)
	push	hl
	ld	hl,(__retaddr)
	push	hl

        ; save kernel stack pointer -- when it comes back in the parent we'll be in
        ; _switchin which will immediately return (appearing to be _dofork()
        ; returning) and with HL (ie return code) containing the child PID.
        ; Hooray.
        ld	(_udata + 14    ), sp

        ; now we're in a safe state for _switchin to return in the parent
        ; process.

        ; --------- copy process ---------
        ld	hl, (fork_proc_ptr)
        ld	de, 15 
        add	hl, de
        push	hl
        call	_copy_and_map_proc
        pop	hl

        ; now the copy operation is complete we can get rid of the stuff
        ; _switchin will be expecting from our copy of the stack.
        ; now the copy operation is complete we can get rid of the stuff
        ; _switchin will be expecting from our copy of the stack.
	; ix/iy are untouched so don't need a restore BC is not so does
	ld	hl, 18
	add	hl, sp
	ld	sp, hl
        pop	bc
	pop	af	; and the pid

        ; Make a new process table entry, etc.
	ld	hl, _udata
	push	hl
        ld	hl, (fork_proc_ptr)
        push	hl
        call	_makeproc
        pop	af
	pop	af

        ; runticks = 0;
        ld	hl, 0
        ld	(_runticks), hl
	;
        ; in the child process, fork() returns zero.
        ;
        ; And we exit, with the kernel mapped, the child now being deemed
        ; to be the live uarea. The parent is frozen in time and space as
        ; if it had done a switchout().
        ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; DEBUGGING
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; mmu_cbar_msg:  .ascii "MMU: CBAR="
;;                .byte 0
;; mmu_cbr_msg:   .ascii ", CBR="
;;                .byte 0
;; mmu_bbr_msg:   .ascii ", BBR="
;;                .byte 0
;; 
;; mmu_state_dump:
;;             ld hl, #mmu_cbar_msg
;;             call outstring
;;             in0 a, (MMU_CBAR)
;;             call outcharhex
;;             ld hl, #mmu_cbr_msg
;;             call outstring
;;             in0 a, (MMU_CBR)
;;             call outcharhex
;;             ld hl, #mmu_bbr_msg
;;             call outstring
;;             in0 a, (MMU_BBR)
;;             call outcharhex
;;             call outnewline
;;             ret
;; ;------------------------------------------------------------------------------
;; dmamsg1:    .ascii "[DMA source="
;;             .byte 0
;; dmamsg2:    .ascii ", dest="
;;             .byte 0
;; dmamsg3:    .ascii ", count="
;;             .byte 0
;; dmamsg4:    .ascii "]"
;;             .byte 13, 10, 0
;; 
;; dump_dma_state:
;;         push af
;;         push hl
;;         push de
;;         push bc
;; 
;;         ld hl, #dmamsg1
;;         call outstring
;; 
;;         in0 a, (DMA_SAR0B)
;;         call outcharhex
;;         in0 a, (DMA_SAR0H)
;;         call outcharhex
;;         in0 a, (DMA_SAR0L)
;;         call outcharhex
;;         
;;         ld hl, #dmamsg2
;;         call outstring
;; 
;;         in0 a, (DMA_DAR0B)
;;         call outcharhex
;;         in0 a, (DMA_DAR0H)
;;         call outcharhex
;;         in0 a, (DMA_DAR0L)
;;         call outcharhex
;; 
;;         ld hl, #dmamsg3
;;         call outstring
;; 
;;         in0 a, (DMA_BCR0H)
;;         call outcharhex
;;         in0 a, (DMA_BCR0L)
;;         call outcharhex
;; 
;;         ld hl, #dmamsg4
;;         call outstring
;; 
;;         pop bc
;;         pop de
;;         pop hl
;;         pop af
;;         ret
;; 
;; 
;; dumpbuf: .ds 16
;; 
;; dump_process_memory:
;;         ; enter with the 64K bank to dump in A (low 4 bits only)
;;         out0 (DMA_SAR0B), a
;; 
;;         ld hl, #0
;;         ld a, #0x02
;;         out0 (DMA_DMODE), a     ; 0x02 - memory to memory, burst mode
;;         xor a
;;         out0 (DMA_SAR0H), a
;;         out0 (DMA_SAR0L), a
;;         ld a, #((0            + FIRST_RAM_BANK) >> 4)
;;         out0 (DMA_DAR0B), a
;; 
;; nextblock:
;;         ; set dest to our target buffer
;;         ld a, #<dumpbuf
;;         out0 (DMA_DAR0L), a
;;         ld a, #>dumpbuf
;;         out0 (DMA_DAR0H), a
;;         ; 16 bytes
;;         xor a
;;         out0 (DMA_BCR0H), a
;;         ld a, #0x10
;;         out0 (DMA_BCR0L), a
;;         ld a, #0x40
;;         out0 (DMA_DSTAT), a     ; 0x40 - enable DMA channel 0
;;         ; DMA does the copy
;; 
;;         ; print address
;;         call outhl
;;         ld a, #':'
;;         call outchar
;;         ld a, #' '
;;         call outchar
;; 
;;         ; print data
;;         ex de, hl
;;         ld hl, #dumpbuf
;;         ld b, #0x10
;; nextbyte:
;;         ld a, (hl)
;;         call outcharhex
;;         ld a, #' '
;;         call outchar
;;         inc hl
;;         djnz nextbyte
;;         ex de, hl
;; 
;;         call outnewline
;; 
;;         ld de, #0x10
;;         add hl, de
;;         ld a, h
;;         or l
;;         jr nz, nextblock
;;         ret
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; -----------------------------------------------------------------------------
; COMMON MEMORY BANK
; -----------------------------------------------------------------------------
	.common

        ; MUST arrange for this table to be 32-byte aligned
        ; linked immediately after commonmem.S
interrupt_table:
        .word	z180_irq1               ;     1    INT1 external interrupt - ? disconnected
        .word	z180_irq2               ;     2    INT2 external interrupt - SD card socket event
        .word	z180_irq3               ;     3    Timer 0
        .word	z180_irq_unused         ;     4    Timer 1
        .word	z180_irq_unused         ;     5    DMA 0
        .word	z180_irq_unused         ;     6    DMA 1
        .word	z180_irq_unused         ;     7    CSI/O
        .word	z180_irq8               ;     8    ASCI 0
        .word	z180_irq9               ;     9    ASCI 1
        .word	z180_irq_unused         ;     10
        .word	z180_irq_unused         ;     11
        .word	z180_irq_unused         ;     12
        .word	z180_irq_unused         ;     13
        .word	z180_irq_unused         ;     14
        .word	z180_irq_unused         ;     15
        .word	z180_irq_unused         ;     16

hw_irqvector:
	.byte	0
_irqvector:
	.byte	0
_need_resched:
	.byte	0
_int_disabled:
	.byte	1

z80_irq:
        push	af
        xor	a
        jr	z180_irqgo

z180_irq1:
        push	af
        ld a, 	1
        jr z180_irqgo

z180_irq2:
        push	af
        ld	a, 2
        jr	z180_irqgo

z180_irq3:
        push	af
        ld a,	3
        ; fall through -- timer is likely to be the most common, we'll save it the jr
z180_irqgo:
        ld (hw_irqvector), a
        ; quick and dirty way to debug which interrupt is jamming us up ...
        ;    add #0x30
        ;    .export outchar
        ;    call outchar
        pop	af
        jp	interrupt_handler

; z180_irq4:
;         push af
;         ld a, #4
;         jr z180_irqgo
; 
; z180_irq5:
;         push af
;         ld a, #5 
;         jr z180_irqgo
; 
; z180_irq6:
;         push af
;         ld a, #6
;         jr z180_irqgo
; 
; z180_irq7:
;         push af
;         ld a, #7
;         jr z180_irqgo

z180_irq8:
        push	af
        ld	a, #8
        jr	z180_irqgo

z180_irq9:
        push	af
        ld	a, 9
        jr	z180_irqgo

; z180_irq10:
;         push af
;         ld a, #10
;         jr z180_irqgo
; 
; z180_irq11:
;         push af
;         ld a, #11
;         jr z180_irqgo
; 
; z180_irq12:
;         push af
;         ld a, #12
;         jr z180_irqgo
; 
; z180_irq13:
;         push af
;         ld a, #13
;         jr z180_irqgo
; 
; z180_irq14:
;         push af
;         ld a, #14
;         jr z180_irqgo
; 
; z180_irq15:
;         push af
;         ld a, #15
;         jr z180_irqgo
; 
; z180_irq16:
;         push af
;         ld a, #16
;         jr z180_irqgo
z180_irq_unused:
        push	af
        ld	a, 0xFF
        jr	z180_irqgo


;
;	Swapping requires the swap out is done on a different stack so we
;	can flip the common of the victim for the write out
;
_swapout:
	pop	de
	pop	hl
	push	hl
	push	de		; HL is now the process to swap
	push	bc
	ld	b,h		; Save the process pointer
	ld	c,l
	ld	de,15 
	add	hl,de
	ld	a,(hl)		; Get the page info for this process
	ld	hl,0
	add	hl,sp		; get the current SP into a register pair
	ld 	sp,swapinstack
	in0	e,(MMU_CBR)	; save the old mapping
	out0	(MMU_CBR),a	; Swap the common to the victim
	push	hl		; Save old stack frame
	push	de		; Save old mapping
	push	bc		; Argument for do_swapout
	call	_do_swapout	; Swap the victim out in its own context
	pop	bc		; discard
	pop	bc		; Recover old mapping
	pop	de		; Recover old stack
	ex	de,hl		; DE is now return value, HL the stack
	di
	out0	(MMU_CBR), c	; Restore map first in case we takn an IRQ
	ld	sp,hl		; Switch stack
	ex	de,hl		; Return value back into HL
	pop	bc		; Restore BC
	jp	map_kernel	; Ensure the lower mapping is fixed up if needed

;
; Switchout switches out the current process, finds another that is READY,
; possibly the same process, and switches it in.  When a process is
; restarted after calling switchout, it thinks it has just returned
; from switchout().
_plt_switchout:
	di
        ; save machine state
        ld	hl,	0 ; return code set here is ignored, but _switchin can 
        ; return from either _switchout OR _dofork, so they must both write 
        ; 14     with the following on the stack:
        push	hl	; return code
	push	bc	; register variables
        push	ix
        push	iy
	ld	hl,(__tmp)	; working values for the compiler
	push	hl
	ld	hl,(__hireg)
	push	hl
	ld	hl,(__tmp2)
	push	hl
	ld	hl,(__tmp2+2)
	push	hl
	ld	hl,(__tmp3)
	push	hl
	ld	hl,(__tmp3+2)
	push	hl
	ld	hl,(__retaddr)
	push	hl
        ld	(_udata + 14    ), sp ; this is where the SP is restored in _switchin

        ; no need to stash udata on this platform since common memory is dedicated
        ; to each process.

        ; find another process to run (may select this one again)
        call	_getproc

        push	hl
        call	_switchin

        ; we should never get here
	call	_plt_monitor

badswitchmsg:
	.ascii "_switchin: FAIL"
.byte 13, 10, 0
swapped:
	.ascii "_switchin: SWAPPED"
.byte 13, 10, 0

_switchin:
        di
        pop bc  ; return address
        pop de  ; new process pointer (struct p_tab *)
        push de ; restore stack (WRS: AC thinks this may not be required -- he's probably right!)
        push bc ; restore stack

	ld a,1
	ld (_int_disabled),a

        ; probably not reqired since we're only called from kernel code ...
        ; call map_kernel

        ld hl, #15 
        add hl, de  ; now HL points at the p_page value for the next process

        ; map in the common memory for the new process -- this swaps common
        ; memory and the stack under our feet so let's hope that common memory
        ; contains a copy of this code, eh?


        ld	a, (hl)
	or	a
	jr	nz, is_resident
	; Swap in the new process (and maybe out an old one)
	ld	sp, swapstack
	ei
	xor	a
	ld	(_int_disabled),a
	push	hl
	push	de
	call	_swapper
	pop	de
	pop	hl
	ld	a,1
	ld	(_int_disabled),a
	di

is_resident:
	;
	; We have no valid stack at this point
	;
	ld	a,(hl)
        ; out0 (MMU_BBR), a -- WRS: leave the kernel mapped in
        out0	(MMU_CBR), a

        ; sanity check: u_data->u_ptab matches what we wanted?
        ld	hl, (_udata + 0     ) ; u_data->u_ptab
        or	a                    ; clear carry flag
        sbc	hl, de              ; subtract, result will be zero if DE==IX
        jr	nz, switchinfail

        ; wants optimising up a bit
        ld	ix, (_udata + 0     )
        ; next_process->p_status = 1           
        ld	(ix + 0 ), 1           

        ; Fix the moved page pointers
        ; Just do one byte as that is all we use on this platform
        ld	a, (ix + 15 )
        ld	(_udata + 2     ), a

        ; runticks = 0
        ld	hl, 0
        ld	(_runticks), hl

        ; restore machine state -- note we may be returning from either
        ; _switchout or _dofork
	;
	; Only from here is the stack valid again
	;
        ld	sp, (_udata + 14    )

	pop	hl
        ld	(__retaddr),hl
	pop	hl
	ld	(__tmp3+2),hl
	pop	hl
	ld	(__tmp3),hl
	pop	hl
	ld	(__tmp2+2),hl
	pop	hl
	ld	(__tmp2),hl
	pop	hl
	ld	(__hireg),hl
	pop	hl
	ld	(__tmp),hl
        pop	iy
        pop	ix
	pop	bc
        pop	hl ; return code

        ; enable interrupts, if the ISR isn't already running
        ld	a, (_udata + 16    )
	ld	(_int_disabled), a
        or	a
        ret	nz ; in ISR, leave interrupts off
        ei
        ret	; return with interrupts on

switchinfail:
        ; something went wrong and we didn't switch in what we asked for
        call	outhl
        ld	hl, badswitchmsg
        call	outstring
        jp	_plt_monitor

map_kernel_restore:
map_buffers:
map_kernel_di:
map_kernel: ; map the kernel into the low 60K, leaves common memory unchanged
        push	af
# 887
        ld	a, OS_RAM1
        out0	(MMU_BBR), a
        pop	af
        ret

map_proc_always_di:
map_proc_always: ; map the process into the low 60K based on current common mem (which is unchanged)
        push	af
# 899
        ld	a, (_udata + 2     )
        out0	(MMU_BBR), a



        ; MMU_CBR is left unchanged
        pop	af
        ret

map_for_swap:
# 914
	out0	(MMU_BBR),a	; the page is passed in A, so we just do an out0
# 919
	ret

map_save_kernel:   ; save the current process/kernel mapping
        push	af
        in0	 a, (MMU_BBR)
        ld	(map_store), a
# 929
        ld	a, OS_RAM1
        out0	(MMU_BBR), a
        pop	af
        ret

map_restore: ; restore the saved process/kernel mapping
        push	af
# 940
        ld	a, (map_store)
        out0	(MMU_BBR), a



        pop	 af
        ret

map_store:  ; storage for map_save/map_restore
        .byte	0


	.common

	; Combining these is tricky so for the sake of 128 bytes or so we
	; don't try at this point.
	.ds	128
swapstack:
	.ds	128
swapinstack:
