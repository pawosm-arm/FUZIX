# 1 "rcbus-z180.S"
;
;	SC126 support code. Based closely upon the N8VEM mark IV code by
;	Will Sowerbutts
;
        .z180

        ; exported symbols
        .export init_early
        .export init_hardware
        .export _detect_1mb
        .export inchar
        .export outchar
        .export plt_interrupt_all
        .export _bufpool
	.export _plt_idle
	.export _reverse_byte
	.export _sd_spi_rx_sector
	.export _sd_spi_tx_sector
# 1 "kernelu.def"
;
U_DATA__TOTALSIZE           .equ 0x200        ; 256+256 bytes @ F600
Z80_TYPE                    .equ 2

OS_BANK                     .equ 0x00         ; value from include/kernel.h

; Memory layout
FIRST_RAM_BANK              .equ 0x80         ; low 512K of physical memory is ROM/ECB window.
Z180_IO_BASE                .equ 0xC0

USE_FANCY_MONITOR           .equ 1            ; disabling this saves around approx 0.5KB
CPU_CLOCK_KHZ               .equ 18432        ; 18.432MHz * 1
TICKSPERSEC                 .equ 40           ; timer interrupt rate (Hz)
TCR_CLOCK		    .equ 23040

PROGBASE		    .equ 0x0000
PROGLOAD		    .equ 0x0100
# 1 "../../cpu-z180/z180.def"
; ASCI serial ports
ASCI_CNTLA0                 .equ Z180_IO_BASE+0x00     ; ASCI control register A channel 0
ASCI_CNTLA1                 .equ Z180_IO_BASE+0x01     ; ASCI control register A channel 1
ASCI_CNTLB0                 .equ Z180_IO_BASE+0x02     ; ASCI control register B channel 0
ASCI_CNTLB1                 .equ Z180_IO_BASE+0x03     ; ASCI control register B channel 0
ASCI_STAT0                  .equ Z180_IO_BASE+0x04     ; ASCI status register    channel 0
ASCI_STAT1                  .equ Z180_IO_BASE+0x05     ; ASCI status register    channel 1
ASCI_TDR0                   .equ Z180_IO_BASE+0x06     ; ASCI transmit data reg, channel 0
ASCI_TDR1                   .equ Z180_IO_BASE+0x07     ; ASCI transmit data reg, channel 1
ASCI_RDR0                   .equ Z180_IO_BASE+0x08     ; ASCI receive data reg,  channel 0
ASCI_RDR1                   .equ Z180_IO_BASE+0x09     ; ASCI receive data reg,  channel 0
ASCI_ASEXT0                 .equ Z180_IO_BASE+0x12     ; ASCI extension register channel 0
ASCI_ASEXT1                 .equ Z180_IO_BASE+0x13     ; ASCI extension register channel 1
ASCI_ASTC0L                 .equ Z180_IO_BASE+0x1A     ; ASCI time constant register channel 0 low
ASCI_ASTC0H                 .equ Z180_IO_BASE+0x1B     ; ASCI time constant register channel 0 high
ASCI_ASTC1L                 .equ Z180_IO_BASE+0x1C     ; ASCI time constant register channel 1 low
ASCI_ASTC1H                 .equ Z180_IO_BASE+0x1D     ; ASCI time constant register channel 1 high

; Z180 MMU
MMU_CBR                     .equ Z180_IO_BASE+0x38     ; common1 base register
MMU_BBR                     .equ Z180_IO_BASE+0x39     ; bank base register
MMU_CBAR                    .equ Z180_IO_BASE+0x3A     ; common/bank area register

; Z180 DMA engine
DMA_SAR0L                   .equ Z180_IO_BASE+0x20     ; DMA source address reg, channel 0L
DMA_SAR0H                   .equ Z180_IO_BASE+0x21     ; DMA source address reg, channel 0H
DMA_SAR0B                   .equ Z180_IO_BASE+0x22     ; DMA source address reg, channel 0B
DMA_DAR0L                   .equ Z180_IO_BASE+0x23     ; DMA dest address reg,   channel 0L
DMA_DAR0H                   .equ Z180_IO_BASE+0x24     ; DMA dest address reg,   channel 0H
DMA_DAR0B                   .equ Z180_IO_BASE+0x25     ; DMA dest address reg,   channel 0B
DMA_BCR0L                   .equ Z180_IO_BASE+0x26     ; DMA byte count reg,     channel 0L
DMA_BCR0H                   .equ Z180_IO_BASE+0x27     ; DMA byte count reg,     channel 0H
DMA_MAR1L                   .equ Z180_IO_BASE+0x28     ; DMA memory address reg, channel 1L
DMA_MAR1H                   .equ Z180_IO_BASE+0x29     ; DMA memory address reg, channel 1H
DMA_MAR1B                   .equ Z180_IO_BASE+0x2A     ; DMA memory address reg, channel 1B
DMA_IAR1L                   .equ Z180_IO_BASE+0x2B     ; DMA I/O address reg,    channel 1L
DMA_IAR1H                   .equ Z180_IO_BASE+0x2C     ; DMA I/O address reg,    channel 1H
DMA_BCR1L                   .equ Z180_IO_BASE+0x2E     ; DMA byte count reg,     channel 1L
DMA_BCR1H                   .equ Z180_IO_BASE+0x2F     ; DMA byte count reg,     channel 1H
DMA_DSTAT                   .equ Z180_IO_BASE+0x30     ; DMA status register
DMA_DMODE                   .equ Z180_IO_BASE+0x31     ; DMA mode register
DMA_DCNTL                   .equ Z180_IO_BASE+0x32     ; DMA/WAIT control register

; Z180 Timer
TIME_TMDR0L                 .equ Z180_IO_BASE+0x0C     ; Timer data register,    channel 0L
TIME_TMDR0H                 .equ Z180_IO_BASE+0x0D     ; Timer data register,    channel 0H
TIME_RLDR0L                 .equ Z180_IO_BASE+0x0E     ; Timer reload register,  channel 0L
TIME_RLDR0H                 .equ Z180_IO_BASE+0x0F     ; Timer reload register,  channel 0H
TIME_TCR                    .equ Z180_IO_BASE+0x10     ; Timer control register
TIME_TMDR1L                 .equ Z180_IO_BASE+0x14     ; Timer data register,    channel 1L
TIME_TMDR1H                 .equ Z180_IO_BASE+0x15     ; Timer data register,    channel 1H
TIME_RLDR1L                 .equ Z180_IO_BASE+0x16     ; Timer reload register,  channel 1L
TIME_RLDR1H                 .equ Z180_IO_BASE+0x17     ; Timer reload register,  channel 1H
TIME_FRC                    .equ Z180_IO_BASE+0x18     ; Timer Free running counter

; CSIO
CSIO_CNTR		    .equ Z180_IO_BASE+0x0A     ; CSIO control/status register
CSIO_TRDR		    .equ Z180_IO_BASE+0x0B     ; CSIO transmit/receive data register

; Z180 Interrupts
INT_IL                      .equ Z180_IO_BASE+0x33     ; Interrupt vector low register
INT_ITC                     .equ Z180_IO_BASE+0x34     ; Interrupt vector low register

; Refresh control
MEM_RCR			    .equ Z180_IO_BASE+0x36	; Refresh control

; ESCC serial ports (Z80182)
ESCC_CTRL_A                 .equ 0xE0                   ; ESCC Channel A control register
ESCC_DATA_A                 .equ 0xE1                   ; ESCC Channel A data register
ESCC_CTRL_B                 .equ 0xE2                   ; ESCC Channel B control register
ESCC_DATA_B                 .equ 0xE3                   ; ESCC Channel B data register

PORT_A_DDR                  .equ 0xED                   ; Port A data direction register
PORT_A_DATA                 .equ 0xEE                   ; Port A data register
PORT_B_DDR                  .equ 0xE4                   ; Port B data direction register
PORT_B_DATA                 .equ 0xE5                   ; Port B data register
PORT_C_DDR                  .equ 0xDD                   ; Port C data direction register
PORT_C_DATA                 .equ 0xDE                   ; Port C data register

Z182_SYSCONFIG              .equ 0xEF                   ; System Configuration Register
Z182_RAMUBR                 .equ 0xE6                   ; RAM upper boundary register
Z182_RAMLBR                 .equ 0xE7                   ; RAM lower boundary register
Z182_ROMBR                  .equ 0xE8                   ; ROM boundary register

; Debugging
DEBUGBANK   .equ 0
DEBUGCOMMON .equ 0
# 1 "../../cpu-z80u/kernel-z80.def"
 
# 26
 
# 44
 
# 24 "rcbus-z180.S"
	.buffers

_bufpool:
        .ds	520  * 4 ; adjust NBUFS in config.h in line with this

	.discard

init_early:
	jp z180_init_early

init_hardware:
        ; enable ASCI interrupts
        in0	a, (ASCI_STAT0)
        or	0x08                ; enable ASCI0 receive interrupts
        out0	(ASCI_STAT0), a
        in0	a, (ASCI_ASEXT0)
        and	0x7f               ; disable RDRF interrupt inhibit
        out0	(ASCI_ASEXT0), a
        in0	a, (ASCI_STAT1)
        or	0x08                ; enable ASCI1 receive interrupts
        out0	(ASCI_STAT1), a
        in0	a, (ASCI_ASEXT1)
        and	0x7f               ; disable RDRF interrupt inhibit
        out0	(ASCI_ASEXT1), a
        jp z180_init_hardware


_detect_1mb:
        ; Try to enable and probe additional RAM chip
        ; Select U2 for low half of physical memory
        ld	hl, _rtc_shadow
        ld	a, (hl)
        or	0x02
        out	(0x0C), a
        ld	(hl), a

        ; Will need to examine a byte in the top 4K logical region (controlled
        ; by CBR) and this also gives us a convenient 0.
        ld	hl, 0xFF00

        in0	c, (MMU_CBR)
        out0	(MMU_CBR), l

        ld	a, (hl)
        xor	0x55
        ld	(hl), a
        ld	b, a
        ld	a, (hl)

        out0	(MMU_CBR), c

        xor	b
        ret	nz	; return 0 in L
        inc	l	; return 1
        ret

	.common

; outchar: Wait for UART TX idle, then print the char in A
; destroys: AF
outchar:
        push	bc
        ld	b, a
        ; wait for transmitter to be idle
ocloop: in0	a, (ASCI_STAT0)
        bit	1, a		; and 0x02
        jr	z, ocloop	; loop while busy
        ; now output the char to serial port
        ld	a, b
        out0	(ASCI_TDR0), a
        pop	bc
        ret

; inchar: Wait for character on UART, return in A
; destroys: AF
inchar:
        in0	a, (ASCI_STAT0)
        rlca
        jr	nc, inchar
        in0	a, (ASCI_RDR0)
        ret

plt_interrupt_all:
        ret

	.code

_plt_idle:
	ld	a,0xFF
	out	(0x0D),a
	halt
	ret

_reverse_byte:
        ld	hl, 2
        add	hl, sp
        ld	a, (hl)
reverse_byte_a:
        ; reverse bits in A
        ld	l,a    ; a = 76543210
        rlca
        rlca      ; a = 54321076
        xor	l
        and	0xAA
        xor	l     ; a = 56341270
        ld	l,a
        rlca
        rlca
        rlca      ; a = 41270563
        rrc	l     ; l = 05634127
        xor	l
        and	0x66
        xor	l     ; a = 01234567
        ld	l, a   ; return value in L
        ret

;****************************************************************************
;* The innermost part of the transfer routines has to live in common memory *
;* since it must be able to bank switch to the user memory bank.            *
;****************************************************************************

	.common


 

_sd_spi_rx_sector:
        pop	hl
        pop	de
        push	de
        push	hl
	push	bc
waitrx: 
        in0	a, (CSIO_CNTR)	; wait for any current tx or rx operation to complete
;FIXME        tst	a, 0x30
        jr	nz, waitrx
        set	5, a		; set CSIO_CNTR_RE, enable rx operation for first byte
        out0	(CSIO_CNTR), a
        ld	h, a		; stash value for reuse later

        ; load parameters
        ld	a, (_td_raw) 	; blkparam.is_user
        ld	bc, 512		; sector size

        cp	2
        jr	nz, not_swapin
        ld	a,(_td_page)
        call	map_for_swap
        jr	rxnextbyte
not_swapin:

        or	a
        jr	z, rd_kernel
        call	map_proc_always	; map user process
        jr	rxnextbyte
rd_kernel:
        call	map_buffers
rxnextbyte:
        dec	bc		; length--
        ld	a, b
        or	c
        jr	nz, waitrx2
        res	5, h		; final byte: clear CSIO_CNTR_RE bit in H
waitrx2:
        ld	l, c		; store C temporarily
        ld	c, CSIO_CNTR	; load IO port address
waitrx3:
        tstio	0x20		; test bits in IO port (C)
        jr	nz, waitrx3	; wait for rx to complete
        in0	a, (CSIO_TRDR)	; load rxd byte
        out0	(CSIO_CNTR), h	; start next rx (or NOP, if this is the final byte)
        ld	c, l		; restore C
        ; reverse bits in A
        ld	l,a		; a = 76543210
        rlca
        rlca			; a = 54321076
        xor	l
        and	0xAA
        xor	l		; a = 56341270
        ld	l,a
        rlca
        rlca
        rlca			; a = 41270563
        rrc	l		; l = 05634127
        xor	l
        and	0x66
        xor	l		; a = 01234567
        ld	(de), a		; store reversed byte value
        inc	de		; ptr++
        bit	5, h
        jr	nz, rxnextbyte	; go again if not yet done
        jr transferdone		; we are done

_sd_spi_tx_sector:
        ; load parameters
        pop	hl
        pop	de
        push	de
        push	hl
	push	bc
        ld	a, (_td_raw)	; blkparam.is_user
        ld	hl, 512		; sector size

        cp	2
        jr	nz, not_swapout
        ld	a, (_td_page)
        call	map_for_swap
        jr	gotx
not_swapout:

        or	a
        jr	z, wr_kernel
        call	map_proc_always	; map user process
        jr	gotx
wr_kernel:
        call	map_buffers
gotx:
        in0	a, (CSIO_CNTR)
        and	0xDF		; mask off RE bit
        or	0x10		; set TE bit
        ld	b, a		; B now contains CNTR register value to start transmission
txnextbyte:
        ld	a, (de)
        ; reverse bits in A
        ld	c,a
        rlca
        rlca
        xor	c
        and	0xAA
        xor	c
        ld	c,a
        rlca
        rlca
        rlca
        rrc	c
        xor	c
        and	0x66
        xor	c
        ld	c, CSIO_CNTR	; load IO port address
waittx: 
        tstio	0x10		; test bits in IO port (C)
        jr	nz, waittx	; wait for tx to complete
        out0	(CSIO_TRDR), a	; write byte to tx
        out0	(CSIO_CNTR), b	; start tx
        inc	de		; ptr++
        dec	hl		; length--
        ld	a, h
        or	l
        jr	nz, txnextbyte	; length != 0, go again
transferdone:			; note this code is shared with sd_spi_rx_block
        ld	l, 1		; return true
	pop	bc
        jp	map_kernel_restore	; map kernel and return
